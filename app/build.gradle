apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.darkminstrel.weatherradar"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 8
        versionName "1.0.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    signingConfigs {
        release {
            def propsFile = new File("../../signing.properties")
            if (!propsFile.exists()) propsFile = new File("../signing.properties")
            if (propsFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propsFile))

                if (props["keystore"] != null && !props["keystore"].isEmpty()) storeFile = file(props["keystore"])
                else println("Keystore shouldn't be empty!")
                keyAlias = props["keyAlias"]
                storePassword = props["keystore.password"]
                keyPassword = props["keyPassword"]
            } else println("Please provide signing credentials in " + propsFile.getAbsolutePath() + " file")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "boolean", "DEBUG_FEATURES", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField "boolean", "DEBUG_FEATURES", "false"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "com.google.android.material:material:1.3.0-alpha02"
    implementation "com.jsibbold:zoomage:1.2.0"

    //okhttp
    implementation "com.squareup.okhttp3:okhttp:4.0.0-alpha01"
    implementation "com.squareup.okio:okio:2.2.2"

    //rxjava2
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    //koin
    def koin_version = "2.1.3"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    implementation "org.koin:koin-android-ext:$koin_version"

    //test
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    testImplementation "org.mockito:mockito-inline:2.24.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

}
